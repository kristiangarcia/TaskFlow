plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group = 'com.taskflow'
version = '0.0.1'

repositories {
    mavenCentral()
}

dependencies {
    // PostgreSQL JDBC Driver para Supabase
    implementation 'org.postgresql:postgresql:42.7.3'

    // BCrypt para hasheo de passwords
    implementation 'org.mindrot:jbcrypt:0.4'

    // ControlsFX para validación visual
    implementation 'org.controlsfx:controlsfx:11.2.1'

    // FontAwesomeFX para iconos
    implementation 'de.jensd:fontawesomefx-fontawesome:4.7.0-9.1.2'

    // JasperReports para generación de informes
    implementation 'net.sf.jasperreports:jasperreports:7.0.1'
    implementation 'net.sf.jasperreports:jasperreports-pdf:7.0.0'
    implementation 'net.sf.jasperreports:jasperreports-fonts:7.0.1'
    implementation 'net.sf.jasperreports:jasperreports-charts:7.0.1'
}

javafx {
    version = "25"
    modules = [
        'javafx.base',
        'javafx.graphics',
        'javafx.controls',
        'javafx.fxml',
        'javafx.web'
    ]
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
    sourceCompatibility = JavaVersion.VERSION_24
    targetCompatibility = JavaVersion.VERSION_24
}

application {
    mainClass = 'com.taskflow.Main'
    applicationDefaultJvmArgs = [
        '--enable-native-access=ALL-UNNAMED',
        '--enable-native-access=javafx.graphics'
    ]
}

tasks.named('run') {
    jvmArgs = application.applicationDefaultJvmArgs
}

sourceSets {
    main {
        resources {
            srcDirs = ["src/main/resources"]
            includes = ["**/*.fxml", "**/*.png", "**/*.jpg", "**/*.properties", "**/*.css", "**/*.jasper"]
        }
    }
}

// Tarea para copiar todas las dependencias a la carpeta build/libs/lib
tasks.register('copyDependencies', Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/libs/lib"
}

// Configuración del JAR normal
tasks.jar {
    dependsOn copyDependencies // Hace que siempre se copien las librerías al hacer el jar

    manifest {
        attributes(
            'Main-Class': application.mainClass.get(),
            //Esto crea el "Class-Path" dentro del manifiesto del JAR
            'Class-Path': configurations.runtimeClasspath.files.collect { "lib/" + it.name }.join(' ')
        )
    }
}
